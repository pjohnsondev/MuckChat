plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.7'
}

application {
    // Define the main class for the application
    mainClassName = 'muck.client.App'
}

javafx {
    version = '11'
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.web' ]
}

dependencies {
    implementation project(":core")
    implementation project(":network")

    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:win"
    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:linux"
    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:mac"

    // JavaFX testing
    testImplementation "org.testfx:testfx-core:4.0.16-alpha"
    testImplementation "org.testfx:testfx-junit5:4.0.16-alpha"

    // For Avatar images
    //implementation "com.gluonhq:charm-glisten:6.0.2"
}

// For the Avatar images
/*repositories {
    maven {
        url ("https://nexus.gluonhq.com/nexus/content/repositories/releases/")
    }
}*/

/**
 * This allows us to build a "fat jar" containing the JavaFX components, so that the client jar can be moved to another
 * computer and run on its own
 */
task fatjar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'muck.client.Launcher'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}